/*
 * SPDX-FileCopyrightText: 2013 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.applicationNamespace = "com.google.android.gms"
    ext.basePackageName = "app.revanced"

    ext.cronetVersion = '102.5005.125'
    ext.wearableVersion = '0.1.1'

    ext.kotlinVersion = '1.9.23'
    ext.coroutineVersion = '1.7.3'

    ext.annotationVersion = '1.7.1'
    ext.appcompatVersion = '1.6.1'
    ext.biometricVersion = '1.1.0'
    ext.coreVersion = '1.12.0'
    ext.fragmentVersion = '1.6.2'
    ext.lifecycleVersion = '2.7.0'
    ext.loaderVersion = '1.1.0'
    ext.materialVersion = '1.11.0'
    ext.mediarouterVersion = '1.7.0'
    ext.multidexVersion = '2.0.1'
    ext.navigationVersion = '2.7.7'
    ext.preferenceVersion = '1.2.1'
    ext.recyclerviewVersion = '1.3.2'
    ext.webkitVersion = '1.10.0'

    ext.slf4jVersion = '1.7.36'
    ext.volleyVersion = '1.2.1'
    ext.wireVersion = '4.9.8'

    ext.androidBuildGradleVersion = '8.3.1'

    ext.androidBuildVersionTools = '34.0.0'

    ext.androidMinSdk = 19
    ext.androidTargetSdk = 29
    ext.androidCompileSdk = 34

    ext.localProperties = new Properties()

    try {
        var stream = rootProject.file('local.properties').newDataInputStream()
        ext.localProperties.load(stream)
        stream.close()
    } catch (ignored) {
        // Ignore
    }

    ext.hasModule = (String name, boolean enabledByDefault) -> {
        return ext.localProperties.getProperty("modules." + name, enabledByDefault.toString()).toBoolean()
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidBuildGradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.squareup.wire:wire-gradle-plugin:$wireVersion"
    }
}

allprojects {
    apply plugin: 'idea'

    group = 'org.microg.gms'
    ext.appVersionCode = 241017000
    ext.baseVersion = ext.appVersionCode.toString()[0..1] + '.' + ext.appVersionCode.toString()[2..3] + '.' + ext.appVersionCode.toString()[4..5]
    version = "0.3.0." + ext.baseVersion.replaceAll("\\.", "")
    ext.isReleaseVersion = true
}

subprojects {
    repositories {
        mavenCentral()
        google()
    }
}

