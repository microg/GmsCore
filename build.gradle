/*
 * Copyright 2013-2019 microG Project Team
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.3.21'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: 'idea'
    ext.androidBuildVersionTools = "28.0.3"
    ext.supportLibraryVersion = "28.0.0"
    ext.isReleaseVersion = false
    ext.slf4jVersion = "1.7.25"
}

def androidCompileSdk() { return 28 }

def androidTargetSdk() { return 28 }

def androidMinSdk() { return 14 }

def versionCode() {
    def stdout = new ByteArrayOutputStream()
    exec { commandLine 'git', 'rev-list', '--count', "HEAD"; standardOutput = stdout }
    return Integer.parseInt(stdout.toString().trim())
}

def versionName() {
    def stdout = new ByteArrayOutputStream()
    if (rootProject.file("gradlew").exists())
        exec { commandLine 'git', 'describe', '--tags', '--always', '--dirty'; standardOutput = stdout }
    else // automatic build system, don't tag dirty
        exec { commandLine 'git', 'describe', '--tags', '--always'; standardOutput = stdout }
    return stdout.toString().trim().substring(1)
}

subprojects {
    group = 'org.microg'
    repositories {
        jcenter()
        google()
    }
}

