/*
 * SPDX-FileCopyrightText: 2015 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.squareup.wire'

android {
    namespace "com.android.vending"
    compileSdkVersion androidCompileSdk
    buildToolsVersion "$androidBuildVersionTools"

    defaultConfig {
        versionName vendingAppVersionName
        versionCode vendingAppVersionCode
        minSdkVersion androidMinSdk
        targetSdkVersion androidTargetSdk
    }

    buildTypes {
        debug {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
            }
        }
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
            }
        }
    }

    flavorDimensions = ['target']
    productFlavors {
        "default" {
            dimension 'target'
        }
        "huawei" {
            dimension 'target'
            versionNameSuffix "-hw"
        }
        "huaweilh" {
            dimension 'target'
            versionNameSuffix "-lh"
            versionCode vendingAppVersionCode - 1000
            matchingFallbacks = ['huawei']
        }
    }

    sourceSets {
        main {
            java {
                srcDirs += "build/generated/source/proto/main/java"
            }
        }
    }


    buildFeatures {
        aidl = true
    }

    lintOptions {
        disable 'MissingTranslation', 'GetLocales'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = 1.8
    }
}

dependencies {
    implementation project(':fake-signature')
    implementation project(':play-services-auth')
    implementation project(':play-services-base-core')

    implementation "com.squareup.wire:wire-runtime:$wireVersion"
    implementation "com.android.volley:volley:$volleyVersion"
}

wire {
    kotlin {
        javaInterop = true
    }
}

if (file('user.gradle').exists()) {
    apply from: 'user.gradle'
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.outputFileName = variant.applicationId + "-" + variant.versionCode + variant.versionName.substring(vendingAppVersionName.length()) + ".apk"
    }
}